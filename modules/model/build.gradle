plugins {
    id 'java-library'
    id 'maven-publish'
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-json'
    api 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.springframework.kafka:spring-kafka'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jar {
    enabled = true
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'io.github.simonjamesrowe'
            artifactId = 'model'

            pom {
                name = 'Model'
                description = 'Shared model and DTOs for the backend service'
                url = 'https://github.com/simonjamesrowe/backend'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'simonjamesrowe'
                        name = 'Simon Rowe'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/simonjamesrowe/backend.git'
                    developerConnection = 'scm:git:ssh://github.com/simonjamesrowe/backend.git'
                    url = 'https://github.com/simonjamesrowe/backend'
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/simonjamesrowe/backend")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
            allowInsecureProtocol = false
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    suppressedValidationErrors.add('enforced-platform')
}

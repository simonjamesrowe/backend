plugins {
    id 'org.springframework.boot'
    id 'java'
    id 'org.springframework.boot.aot'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

dependencies {
    implementation project(':modules:model')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation project(':modules:component-test')
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

graalvmNative {
    binaries {
        main {
            imageName = 'search-service'
            mainClass = 'com.simonjamesrowe.searchservice.SearchServiceApplication'
            buildArgs.add('--verbose')
            buildArgs.add('-march=compatibility')
            buildArgs.add('--initialize-at-build-time=org.slf4j.LoggerFactory,org.slf4j.helpers.Reporter,ch.qos.logback.classic.Level,ch.qos.logback.classic.Logger,ch.qos.logback.core.util.Loader,ch.qos.logback.core.util.StatusPrinter,ch.qos.logback.core.util.StatusPrinter2,ch.qos.logback.core.status.StatusBase,ch.qos.logback.core.status.InfoStatus,org.apache.commons.logging.LogFactory')
        }
    }
}

bootBuildImage {
    imageName = "ghcr.io/simonjamesrowe/search-service:${version}"
    environment = [
        'BP_JVM_VERSION': '21',
        'BP_NATIVE_IMAGE': 'true',
        'BP_NATIVE_IMAGE_BUILD_ARGUMENTS': '-march=compatibility'
    ]
    docker {
        publishRegistry {
            url = "https://ghcr.io"
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    createdDate = 'now'
}

test {
    useJUnitPlatform()
}

tasks.named('processTestAot').configure {
    enabled = false
}
